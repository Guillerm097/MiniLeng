/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */
options
{
	ignore_case = true;
	static = true;
}

PARSER_BEGIN(MiniLeng)

package main;
import java.io.IOException;
import java.util.LinkedList;


import lexico.LexicoUtils;

import sintactico.SintacticoUtils;
import static sintactico.SintacticoUtils.error_sintactico;

import semantico.*;
import semantico.excepciones.*;
import semantico.Simbolo.Tipo_variable;
import semantico.Simbolo.Clase_parametro;
import semantico.SemanticoUtils.Tipo_operador;
import semantico.ExprRegister;

import generacioncodigo.Salida;
import static generacioncodigo.Salida.*;


public class MiniLeng
{
	static Tabla_simbolos ts = new Tabla_simbolos();
	static LexicoUtils tl = new LexicoUtils();
	static Salida sout = null;


	public static void main(String args []) throws ParseException
	{
		MiniLeng parser;

		ts.inicializar_tabla();

		System.out.println(args[0]);

		sout = new Salida(args[0] + ".code");

		// DUDA: boolean flagsv = args[args.length - 2].equals("flags") && args[args.length - 1].equals("-v");
		boolean flagsv = false;
		if (args.length == 0 || (args.length == 2 && flagsv)) {
			parser = new MiniLeng(System.in);
		}
		else if (args.length == 1 || (args.length == 3 && flagsv)) {
			try {
				parser = new MiniLeng(new java.io.FileInputStream(args[0] + ".ml"));
			}
			catch(java.io.FileNotFoundException e) {
				System.out.println ("MiniLeng: The file " + args[0] + ".ml" + " was not found.");
				return;
			}
		}

		try
		{
			//MiniLeng.one_line();
			MiniLeng.programa();
			if (flagsv) {
				LexicoUtils.mostrarTabla();
			}

			// if (volcar_codificacion)
			escribirFicheroFinalASM();

		}// ERRORES LEXICOS
		catch (IOException io) {
			System.out.println("No se ha podido escribir el fichero con el codigo ensamblador .code");
		}
		catch (Error e) {
			LexicoUtils.mostrarErrorLexico(MiniLengTokenManager.input_stream.getBeginLine(),
										MiniLengTokenManager.input_stream.getBeginColumn(),
										MiniLengTokenManager.curChar);
		}
	}
}


PARSER_END(MiniLeng)

SKIP :
{
	" "
	| "%%" : WithinBlockComment
	| "%" : WithinLineComment
	| "\r"
	| "\t"
	| "\n"
}

<WithinBlockComment> SKIP :
{
	"%%" : DEFAULT
}

<WithinBlockComment> MORE :
{
	< ~[] >
}

<WithinLineComment> SKIP :
{
	"\n" : DEFAULT
}

<WithinLineComment> MORE :
{
	< ~[] >
}

TOKEN: /* PALABRAS RESERVADAS */
{
	  < tVAL : "val" >			 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tREF : "ref" > 			 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tLEER : "leer" >		 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tTRUE : "true" >		 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tFALSE : "false" >		 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tACCION : "accion" >	 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tENTERO : "entero" >	 {  LexicoUtils.anyadirToken("palabra reservada");
									LexicoUtils.anyadirToken("entero"); 			  }
	| < tENTACAR : "entacar" >	 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tCARAENT : "caraent" > 	 {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tBOOLEANO : "booleano" > {  LexicoUtils.anyadirToken("palabra reservada");
									LexicoUtils.anyadirToken("booleano"); 		  }
	| < tCARACTER : "caracter" > {  LexicoUtils.anyadirToken("palabra reservada");
									LexicoUtils.anyadirToken("caracter"); 		  }
	| < tESCRIBIR : "escribir" > {  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tPROGRAMA : "programa" > {  LexicoUtils.anyadirToken("palabra reservada"); }
}

TOKEN : /* PALABRAS RESERVADAS CONTROL FLUJO */
{
	  < tSI : "si" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tMQ : "mq" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tFIN : "fin" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tENT : "ent" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tFSI : "fsi" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tFMQ : "fmq" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tSI_NO : "si_no" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
	| < tPRINCIPIO : "principio" >	{  LexicoUtils.anyadirToken("palabra reservada"); }
}

TOKEN : /* OPERATORS */
{
	  < tPLUS : "+" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMINUS : "-" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMULTIPLY : "*" >	{  LexicoUtils.anyadirToken("operador"); }
	| < tDIVIDE : "/" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMAYOR : ">" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMENOR : "<" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tIGUAL : "=" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMAI : ">=" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMEI : "<=" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tNI : "<>" > 		{  LexicoUtils.anyadirToken("operador"); }
	| < tOPAS : ":=" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tOR : "or" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tAND : "and" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tNOT : "not" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tMOD : "mod" >		{  LexicoUtils.anyadirToken("operador"); }
	| < tDIV : "div" >		{  LexicoUtils.anyadirToken("operador"); }
}

TOKEN : /* SEPARADORES Y AGRUPADORES */
{
	  < tPC : ";" >
	| < tC  : "," >
	| < tAP : "(" >
	| < tCP : ")" >
}

TOKEN : /* TOKENS NO CONSTANTES */
{

	  < tCONSTCHAR : "\"" ~["\""] "\"" >
	| < tCONSTCAD : "\"" (~["\""])* "\"" >
	| < tCONSTENTERA : (< DIGIT >)+ >
	| < tIDENTIFICADOR : (< LETTER >) | ((< LETTER > | "_") (< LETTER > | < DIGIT > |  "_" )* (< LETTER > | < DIGIT >)) >
	  {  LexicoUtils.anyadirToken("identificador"); }
	| < #DIGIT : [ "0"-"9" ] >
	| < #LETTER : [ "a"-"z", "A"-"Z" ] >

}
/*
int one_line() :
{}
{
(
< tPROGRAMA>
| < tENTERO >
| < tBOOLEANO >
| < tCARACTER >
| < tTRUE >
| < tFALSE >
| < tENTACAR >
| < tCARAENT >
| < tACCION >
| < tVAL >
| < tREF >
| < tESCRIBIR >
| < tLEER >
| < tPRINCIPIO >
| < tFIN >
| < tSI >
| < tENT >
| < tSI_NO >
| < tFSI >
| < tMQ >
| < tFMQ >
| < tPLUS >
| < tMINUS >
| < tMULTIPLY >
| < tDIVIDE >
| < tMAYOR >
| < tMENOR >
| < tIGUAL >
| < tMAI >
| < tMEI >
| < tNI >
| < tOPAS >
| < tAND >
| < tOR >
| < tNOT >
| < tMOD >
| < tDIV >
| < tPC >
| < tC >
| < tAP >
| < tCP >
| < tIDENTIFICADOR >
| < tCONSTENTERA >
| < tCONSTCAD >
| < tCONSTCHAR >
| < EOF > { return 1; }
)+
}*/

// <tPROGRAMA> <tIDENTIFICADOR> ";"
// declaracion_variables() declaracion_acciones() bloque_sentencias() < EOF >
void programa() :
{
  	Token to;
}
{
	try {
		< tPROGRAMA > to = < tIDENTIFICADOR > ";"
		{
		  	ts.introducir_programa(to.image, ts.getDir());
		  	escribirASM("ENP L" + getEtiqueta());
		}
		declaracion_variables()
		declaracion_acciones()
		{
			escribirASM("; Comienzo de " + to.image);
			escribirEtiquetaASM();
		}
		bloque_sentencias()
		{
			escribirASM("; Fin de " + to.image);
		  	escribirASM("LVP");
		}
		< EOF >
  	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// (declaracion() ";")*
void declaracion_variables() :
{}
{
	try {
		(declaracion() ";")*
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// tipo_variables() identificadores()
void declaracion() :
{
  	Tipo_variable tv;
}
{
	try {
		tv = tipo_variables()
		identificadores(tv)
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

/*	  < tENTERO >
	| < tCARACTER >
	| < tBOOLEANO >
*/
Tipo_variable tipo_variables() :
{}
{
	try {
		  < tENTERO > { return Tipo_variable.ENTERO; }
		| < tCARACTER > { return Tipo_variable.CHAR; }
		| < tBOOLEANO > { return Tipo_variable.BOOLEANO; }
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tIDENTIFICADOR > ("," < tIDENTIFICADOR >)*
void identificadores(Tipo_variable tipo) :
{
	Token to1 = null;
	Token to2 = null;
}
{
  	try {
		to1 = < tIDENTIFICADOR >
		{
			ts.introducir_variable(to1.image, tipo, ts.getNivel(), ts.getDir());
			ts.setDir(ts.getDir() + 1);
		}
		("," to2 = < tIDENTIFICADOR >
		{
		  	ts.introducir_variable(to2.image, tipo, ts.getNivel(), ts.getDir());
			ts.setDir(ts.getDir() + 1);
		}
		)*
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// (declaracion_accion())*
void declaracion_acciones() :
{}
{
	try {
		(declaracion_accion())*
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
		   t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// cabecera_accion() ";" declaracion_variables() declaracion_acciones() bloque_sentencias()
void declaracion_accion() :
{}
{
	try {
		cabecera_accion() ";" declaracion_variables() declaracion_acciones() bloque_sentencias()
		{
			int nivel = ts.getNivel();
			ts.eliminar_variables(nivel);
			ts.eliminar_acciones(nivel);
			ts.eliminar_parametros_ocultos(nivel + 1);
			ts.setNivel(nivel - 1);
			ts.setDir(0); // TODO DUDA: Cómo recuperar la @ máx del BA anterior?

			escribirASM("CSF");
		}
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tACCION > <tIDENTIFICADOR > parametros_formales()
void cabecera_accion() :
{
	Token to;
	Simbolo s = null;
	LinkedList <Simbolo> lista_param;
}
{
	try {
		< tACCION >
		to = < tIDENTIFICADOR >
		{
			int nivel = ts.getNivel();
			int dir = ts.getDir();
			try { s = ts.buscar_simbolo(to.image); }
			catch (SimboloNoEncontradoException e) {
				// Hay identificador, si mismo nivel entonces duplicado
				if ((s != null) && (s.getNivel() == nivel))
					throw new IdentificadorDuplicadoException(to.beginLine, to.beginColumn, to.image);
			  	s = ts.introducir_accion(to.image, nivel, ts.getDir());
			}

			ts.setNivel(nivel + 1);
			ts.setDir(3); // Nuevo bloque de activación la dirección pasa a ser 3
		}

		parametros_formales(s)
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
		   t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(IdentificadorDuplicadoException e) {
		noCodificarASM();
		System.out.println(e);
	}
}

// ("(" parametros() ")")?
void parametros_formales(Simbolo acc) :
{
	LinkedList<Simbolo> ls = null;
}
{
	try {

		"(" ls = parametros()
		{
			acc.setLista_parametros(ls);
		}
		(
			";" ls = parametros()
			{
			  	acc.setLista_parametros(ls);
			}
		)*
		")"
		| ("()")?
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// clase_parametros() tipo_variables() lista_parametros()
LinkedList<Simbolo> parametros() :
{
	Token to;
	Clase_parametro c;
	Tipo_variable v;
	LinkedList<Simbolo> ls = new LinkedList<Simbolo>();
}
{
	try {
		c = clase_parametros()

		v = tipo_variables()
		lista_parametros(c, v, ls)
		{
			return ls; // TODO DUDA: Comprobar si paso de parametros esta bien en general
		}
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tIDENTIFICADOR > (";" parametros())*
void lista_parametros(Clase_parametro c, Tipo_variable v, LinkedList<Simbolo> ls) :
{
	Token to1, to2;
	Simbolo s1 = new Simbolo();
	Simbolo s2 = new Simbolo();
}
{
	try {
		to1 = < tIDENTIFICADOR >
		{
			// TODO: esto va en argumentos()
			// Un parámetro pasado por valor no puede ser pasado por referencia en una nueva acción
			/*s1 = ts.buscar_simbolo(to1.image);
			if ((s1.es_valor()) && (c == Clase_parametro.REF))
				throw new AsignacionParametroPorValorException(to1.beginLine, to1.beginColumn, to1.image);*/

			try {
				s1 = ts.buscar_simbolo(to1.image);
			}
			catch(SimboloNoEncontradoException e) {
				if (s1.es_parametro())
					throw new IdentificadorDuplicadoException(to1.beginLine, to1.beginColumn, to1.image);
			}

			ts.introducir_parametro(to1.image, v, c, ts.getNivel(), ts.getDir());
			ts.setDir(ts.getDir() + 1);

			ls.add(s1);
		}
		("," to2 = <tIDENTIFICADOR>
		{
			try {
				s2 = ts.buscar_simbolo(to2.image);
			}
			catch(SimboloNoEncontradoException e) {
				if (s2.es_parametro())
					throw new IdentificadorDuplicadoException(to2.beginLine, to2.beginColumn, to2.image);
			}

			s2.introducir_parametro(to2.image, v, c, ts.getNivel(), ts.getDir());
			ts.setDir(ts.getDir() + 1);
			
			ls.add(s2);
		}
		)*
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	/*TODO:catch(AsignacionParametroPorValorException e) {
		noCodificarASM();
		System.out.println(e);
	}*/
	catch(IdentificadorDuplicadoException e) {
	  	noCodificarASM();
	  	System.out.println(e);
	}
}

/*
	< tVAL >
    | < tREF >
*/
Clase_parametro clase_parametros() :
{}
{
	try {
		  < tVAL > { return Clase_parametro.VAL; }
		| < tREF > { return Clase_parametro.REF; }
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tPRINCIPIO > lista_sentencias() < tFIN >
void bloque_sentencias() :
{}
{
	try {
		< tPRINCIPIO > lista_sentencias() < tFIN >
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// (sentencia())*
void lista_sentencias() :
{}
{
	try {
		(sentencia())+
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

/*
	leer() ";"
	| escribir() ";"
	| < tIDENTIFICADOR > (resto_asignacion() | resto_invocacion_accion()) // Hay que factorizar
	| seleccion()
	| mientras_que()
*/
void sentencia() :
{
	Simbolo s;
	Token to;
}
{
	try {
		  leer() ";"
		| escribir() ";"
		| to = < tIDENTIFICADOR > (resto_asignacion() | resto_invocacion_accion()) // Hay que factorizar
		{
			s = ts.buscar_simbolo(to.image);
			escribirASM("SRF " + ts.getNivel() + " " + s.getDir());
			escribirASM("DRF");
		}
		| seleccion()
		| mientras_que()
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(SimboloNoEncontradoException e) {
	  	noCodificarASM();

		Token t;
	  	t = getNextToken();
		e.restoSimboloNoEncontrado(t.beginLine, t.beginColumn);
		System.out.println(e);
	}
}

// < tOPAS > expresion() ";"
void resto_asignacion() :
{}
{
	try {
		< tOPAS > expresion() ";"
		{
			escribirASM("ASG");
		}
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// (argumentos())? ";"
void resto_invocacion_accion() :
{}
{
	try {
		(argumentos())? ";"
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tLEER > "(" lista_asignables() ")"
void leer() :
{
	String cadLeer;
}
{
	try {
		< tLEER >
		"(" cadLeer = lista_asignables() ")"
		{
			escribirASM(cadLeer);
		}
	}
	catch(ParseException e) {
		noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tidentificador > ("," < tidentificador >)*
String lista_asignables() :
{
  	String cadLeer;
	Token to1, to2;
	Simbolo s1, s2;
	Tipo_variable var1, var2;
	int nivelSimbolo;
}
{
	try {
		to1 = < tIDENTIFICADOR >
		{
			s1 = ts.buscar_simbolo(to1.image);
			var1 = s1.getVariable();
			if ((s1.es_parametro() &&
				(var1 != Tipo_variable.ENTERO) && (var1 != Tipo_variable.CHAR) &&
				(var1 != Tipo_variable.DESCONOCIDO)))
					throw new TipoInvalidoLeerException(to1.beginLine, to1.beginColumn, to1.image, var1);

			if (s1.es_valor())
				throw new AsignacionParametroPorValorException(to1.beginLine, to1.beginColumn, to1.image);

			cadLeer = "SRF " + ts.getNivel() + " " + s1.getDir() + '\n';
			if (s1.getVariable() == Tipo_variable.CHAR){
				cadLeer += "RD 0\n";
			}
			else {
				cadLeer += "RD 1\n";
			}
		}
		("," to2 = < tIDENTIFICADOR >
		{
			s2 = ts.buscar_simbolo(to2.image);
			var2 = s2.getVariable();
			if ((s2.es_parametro() &&
				(var2 != Tipo_variable.ENTERO) && (var2 != Tipo_variable.CHAR) &&
				(var2 != Tipo_variable.DESCONOCIDO)))
				throw new TipoInvalidoLeerException(to2.beginLine, to2.beginColumn, to2.image, var2);

			if (s2.es_valor())
				throw new AsignacionParametroPorValorException(to2.beginLine, to2.beginColumn, to2.image);


			cadLeer += "SRF " + ts.getNivel() + " " + s2.getDir() + '\n';
			if (s2.getVariable() == Tipo_variable.CHAR){
				cadLeer += "RD 0\n";
			}
			else {
				cadLeer += "RD 1\n";
			}
		}
		)*
		{
	  		return cadLeer;
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(SimboloNoEncontradoException e) {
	  	noCodificarASM();

		Token t;
	  	t = getNextToken();
		e.restoSimboloNoEncontrado(t.beginLine, t.beginColumn);
		System.out.println(e);
	}
	catch(TipoInvalidoLeerException e) {
		noCodificarASM();
		System.out.println(e);
	}
	catch(AsignacionParametroPorValorException e) {
	  	noCodificarASM();
		System.out.println(e);
	}
}

//  tESCRIBIR >  "(" lista_escribibles()  ("," lista_escribibles())* 

void escribir() :
{
	String cadEscribir;
}
{
	try {
		< tESCRIBIR >
		"(" cadEscribir = lista_escribibles() ")"
		{
			escribirASM(cadEscribir);
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

/*
	(
	  < tIDENTIFICADOR >
	| < tCONSTCHAR >
	| < tCONSTCAD >
	| < tENTACAR >  "(" < tCONSTENTERA > ")"
	) ("," lista_escribibles())*
*/
String lista_escribibles() :
{
	String cadEscribir = null;
	Simbolo s1, s2;
	Token to1, to2;
}
{
	try {
	  	(	  	  to1 = < tENTACAR >  "(" < tCONSTENTERA > ")"
	    | to1 = < tIDENTIFICADOR >
		{
			s1 = ts.buscar_simbolo(to1.image);
			cadEscribir += "SRF " + ts.getNivel() + " " + s1.getDir() + '\n';
			cadEscribir += "DRF\n";
			if (s1.getVariable() == Tipo_variable.CHAR) {
				cadEscribir += "WRT 0\n";
			}
			else {
				cadEscribir += "WRT 1\n";
			}
		}
		| to1 = < tCONSTCHAR >
		| to1 = < tCONSTCAD >
		)
		("," cadEscribir = lista_escribibles())*
		{
		  return cadEscribir;
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(SimboloNoEncontradoException e) {
	  	noCodificarASM();

		Token t;
	  	t = getNextToken();
		e.restoSimboloNoEncontrado(t.beginLine, t.beginColumn);
		System.out.println(e);
	}
}

// INUTIL: CREABA CONFLICTOS; ahora resto_asignacion()
// < tIDENTIFICADOR > < tOPAS > expresion() ";"
void asignacion() :
{
	Token to;
	Simbolo s;
	Tipo_variable tVar;
	ExprRegister expr;
}
{
	try {
		to = < tIDENTIFICADOR >
		{
			s = ts.buscar_simbolo(to.image);
		}
		< tOPAS >
		expr = expresion() ";"
		{
			if (s.es_valor()) {
				throw new AsignacionParametroPorValorException(to.beginLine, to.beginColumn, to.image);
			}

			if (s.getVariable() != expr.tipo) {
				throw new AsignacionDiferentesTiposException(to.beginLine, to.beginColumn,
						to.image, s.getVariable(), expr.tipo);
			}
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(SimboloNoEncontradoException e) {
	  	noCodificarASM();

		Token t;
	  	t = getNextToken();
		e.restoSimboloNoEncontrado(t.beginLine, t.beginColumn);
		System.out.println(e);
	}
	catch(AsignacionParametroPorValorException e) {
	  	noCodificarASM();
		System.out.println(e);
	}
	catch(AsignacionDiferentesTiposException e) {
	  	noCodificarASM();
		System.out.println(e);
	}
}

// < tIDENTIFICADOR > (argumentos()) ";"
void invocacion_accion() : // INUTIL PORQUE CAUSABA CONFLICTO
{}
{
	try {
		< tIDENTIFICADOR > (argumentos()) ";"
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// < tMQ > expresion() lista_sentencias() < tFMQ >
void mientras_que() :
{
	ExprRegister expr;
	int etiqINI, etiqFIN;
}
{
	try {
		< tMQ >
		{
			etiqINI = nuevaEtiqueta();
			escribirEtiquetaASM(etiqINI);
		}
		expr = expresion()
		{
			if ((expr.tipo != Tipo_variable.BOOLEANO) && (expr.tipo != Tipo_variable.DESCONOCIDO)){
				throw new TipoCondicionInvalidoException(expr.fila, expr.columna, expr.tipo);
			}

			etiqFIN = nuevaEtiqueta();
			escribirASM("JMF " + "L" + etiqFIN);
		}
		lista_sentencias()
		< tFMQ >
		{
			escribirASM("JMP " + "L" + etiqINI);
			escribirEtiquetaASM(etiqFIN);
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(TipoCondicionInvalidoException e) {
		noCodificarASM();
		System.out.println(e);
	}
}

/*
	< tSI > expresion() < tENT > lista_sentencias()
	(< tSI_NO > lista_sentencias())?
	< tFSI >
*/
void seleccion() :
{
	ExprRegister expr;
	int etiqSINO, etiqFIN;
}
{
	try {
		< tSI > expr = expresion()
		{
			if ((expr.tipo != Tipo_variable.BOOLEANO) && (expr.tipo != Tipo_variable.DESCONOCIDO)){
				throw new TipoCondicionInvalidoException(expr.fila, expr.columna, expr.tipo);
			}

			etiqSINO = nuevaEtiqueta();
			escribirASM("JMF L" + etiqSINO);
		}
		< tENT >
		lista_sentencias()
		{
			etiqFIN = nuevaEtiqueta();
			escribirASM("JMP L" + etiqFIN);
			escribirEtiquetaASM(etiqSINO);
		}
		(
			< tSI_NO >
			lista_sentencias()
		)?
		< tFSI >
		{
			escribirEtiquetaASM(etiqFIN);
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(TipoCondicionInvalidoException e) {
		noCodificarASM();
		System.out.println(e);
	}
}

// "(" (lista_expresiones())? ")"
void argumentos() :
{}
{
	try {
		"(" (lista_expresiones())? ")"
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// (expresion())+
void lista_expresiones() :
{}
{
	try {
		expresion() ("," expresion())*
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// expresion_simple() (operador_relacional() expresion_simple())*
ExprRegister expresion() :
{
	ExprRegister exprIzq, exprDer, resultado;
	Tipo_variable varExprIzq, varExprDer;
	Tipo_operador op;
}
{
	try {
		exprIzq = expresion_simple()
		(
		op = operador_relacional()
		exprDer = expresion_simple()
		{
			varExprIzq = exprIzq.tipo;
		  	varExprDer = exprDer.tipo;
		  	resultado = new ExprRegister();
			resultado.tipo = Tipo_variable.BOOLEANO;
			resultado.fila = exprIzq.fila;
			resultado.fila = exprDer.fila;

			switch(op) { // DUDA: Las expresiones solo permiten comparar enteros??? Y booleanos???
				case MAYOR:
				if (varExprIzq != Tipo_variable.ENTERO && varExprIzq != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
				}
				if (varExprDer != Tipo_variable.ENTERO && varExprDer != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
				}

				if (exprIzq.val_entero > exprDer.val_entero) resultado.val_booleano = true;
				else resultado.val_booleano = false;

				escribirASM("GT");

				return resultado;

				case MENOR:
				if (varExprIzq != Tipo_variable.ENTERO && varExprIzq != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
				}
				if (varExprDer != Tipo_variable.ENTERO && varExprDer != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
				}

				if (exprIzq.val_entero < exprDer.val_entero) resultado.val_booleano = true;
				else resultado.val_booleano = false;

				escribirASM("LT");

				return resultado;

				case IGUAL:
				if (varExprIzq != Tipo_variable.ENTERO && varExprIzq != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
				}
				if (varExprDer != Tipo_variable.ENTERO && varExprDer != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
				}

				if (exprIzq.val_entero == exprDer.val_entero) resultado.val_booleano = true;
				else resultado.val_booleano = false;

				escribirASM("EQ");

				return resultado;

				case MAI:
				if (varExprIzq != Tipo_variable.ENTERO && varExprIzq != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
				}
				if (varExprDer != Tipo_variable.ENTERO && varExprDer != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
				}

				if (exprIzq.val_entero >= exprDer.val_entero) resultado.val_booleano = true;
				else resultado.val_booleano = false;

				escribirASM("GTE");

				return resultado;

				case MEI:
				if (varExprIzq != Tipo_variable.ENTERO && varExprIzq != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
				}
				if (varExprDer != Tipo_variable.ENTERO && varExprDer != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
				}

				if (exprIzq.val_entero <= exprDer.val_entero) resultado.val_booleano = true;
				else resultado.val_booleano = false;


				escribirASM("LTE");

				return resultado;

				case NI:
				if (varExprIzq != Tipo_variable.ENTERO || varExprIzq != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
				}
				if (varExprDer != Tipo_variable.ENTERO || varExprDer != Tipo_variable.DESCONOCIDO) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
				}

				if (exprIzq.val_entero != exprDer.val_entero) resultado.val_booleano = true;
				else resultado.val_booleano = false;


				escribirASM("NEQ");

				return resultado;

				default:
				return resultado;
			}
		}
		)*
		{
		  	return exprIzq;
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(TipoMiembroExpresionInvalidoException e) {
		noCodificarASM();
		System.out.println(e);
	}
}

/*
	  < tMAYOR >
	| < tMENOR >
	| < tIGUAL >
	| < tMAI >
	| < tMEI >
	| < tNI >
*/
Tipo_operador operador_relacional() :
{}
{
	try {
		  < tMAYOR > { return Tipo_operador.MAYOR; }
		| < tMENOR > { return Tipo_operador.MENOR; }
		| < tIGUAL > { return Tipo_operador.IGUAL; }
		| < tMAI > { return Tipo_operador.MAI; }
		| < tMEI > { return Tipo_operador.MEI; }
		| < tNI > { return Tipo_operador.NI; }
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// termino() (operador_aditivo() termino())*
ExprRegister expresion_simple() :
{
	ExprRegister exprIzq, exprDer;
	ExprRegister resultado = new ExprRegister();
	Tipo_variable varExprIzq, varExprDer;
	Tipo_operador op;
}
{
	try {
		exprIzq = termino()
		(
		op = operador_aditivo()
		exprDer = termino()
		{
			// TODO: Propagar fila y columna
			varExprIzq = exprIzq.tipo; System.out.println(exprIzq);
			varExprDer = exprDer.tipo;

			switch(op) {
				case SUMA:
				if ((varExprIzq != Tipo_variable.ENTERO)
					&& (varExprIzq != Tipo_variable.DESCONOCIDO)) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
	  			}

				if ((varExprDer != Tipo_variable.ENTERO)
	  				&& (varExprDer != Tipo_variable.DESCONOCIDO)) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
	  			}

				if ((varExprDer == Tipo_variable.ENTERO)
					&& (varExprIzq == Tipo_variable.ENTERO)) {
					resultado.val_entero = exprIzq.val_entero + exprDer.val_entero;
					resultado.tipo = Tipo_variable.ENTERO;
				}
				else {
					resultado.tipo = Tipo_variable.DESCONOCIDO;
				}

				escribirASM("PLUS");

	  			break;

		  		case RESTA:
				if ((varExprIzq != Tipo_variable.ENTERO)
	  				&& (varExprIzq != Tipo_variable.DESCONOCIDO)) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "entero, desconocido", "izquierdo");
	  			}

				if ((varExprDer != Tipo_variable.ENTERO)
	  				&& (varExprDer != Tipo_variable.DESCONOCIDO)) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "entero, desconocido", "derecho");
	  			}

				if ((varExprDer == Tipo_variable.ENTERO)
					&& (varExprIzq == Tipo_variable.ENTERO)) {
					resultado.val_entero = exprIzq.val_entero - exprDer.val_entero;
					resultado.tipo = Tipo_variable.ENTERO;
				}
				else {
					resultado.tipo = Tipo_variable.DESCONOCIDO;
				}

				escribirASM("SBT");

	  			break;

		  		case OR:
				if ((varExprIzq != Tipo_variable.BOOLEANO)
	  				&& (varExprIzq != Tipo_variable.DESCONOCIDO)) {
					throw new TipoMiembroExpresionInvalidoException(exprIzq.fila,
							exprIzq.columna, varExprIzq, "booleano, desconocido", "izquierdo");
	  			}

				if ((varExprDer != Tipo_variable.BOOLEANO)
	  				&& (varExprDer != Tipo_variable.DESCONOCIDO)) {
					throw new TipoMiembroExpresionInvalidoException(exprDer.fila,
							exprDer.columna, varExprDer, "booleano, desconocido", "derecho");
	  			}

				if ((varExprDer == Tipo_variable.BOOLEANO)
					&& (varExprIzq == Tipo_variable.BOOLEANO)) {
					resultado.val_booleano = exprIzq.val_booleano | exprDer.val_booleano;
					resultado.tipo = Tipo_variable.BOOLEANO;
				}
				else {
					resultado.tipo = Tipo_variable.DESCONOCIDO;
				}

				escribirASM("OR");

	  			break;

	  			default:
	  			return resultado;
			}
		}
		)*
		{
		  	return resultado;
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(TipoMiembroExpresionInvalidoException e) {
		noCodificarASM();
		System.out.println(e);
	}
}

/*
	  "+"
	| "-"
	| < tOR >
*/
Tipo_operador operador_aditivo() :
{}
{
	try {
		  "+" 		{ return Tipo_operador.SUMA; }
		| "-"		{ return Tipo_operador.RESTA; }
		| < tOR >	{ return Tipo_operador.OR; }
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

// factor() (operador_multiplicativo() factor())*
ExprRegister termino() :
{
  	ExprRegister expr1, expr2, resultado;
	String tOP;
}
{
	try {
		expr1 = factor()
		(
		tOP = operador_multiplicativo() expr2 = factor()
		{
			resultado = new ExprRegister();
			// DUDA: Esta bien guardar operador multiplicativo para escribir despues??? (notacion polaca)
			escribirASM(tOP);
			
			if (expr1.tipo == Tipo_variable.BOOLEANO) {
				//TODO operador resultado.val_booleano = expr1.val_booleano  expr2.val_booleano;
				resultado.tipo = Tipo_variable.BOOLEANO;
			}
			else {
			  	//TODO operador resultado.val_entero = expr1.val_entero  expr2.val_entero;
				resultado.tipo = Tipo_variable.ENTERO;
			}
			resultado.fila = expr1.fila;
			resultado.columna = expr1.columna;

			return resultado;
		}
		)*
		{
			// TODO: Revisar si basta con mirar el primer factor: ¿Solo entero y booleano?
			if ((expr1.tipo == Tipo_variable.ENTERO) || (expr1.tipo == Tipo_variable.BOOLEANO)) {
				return expr1;
			} // TODO: else throw Exception
			else {
			  	System.out.println("EXCEPCION");
				return new ExprRegister();
			}
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

/*
	  "*"
	| "/"
	| < tDIV >
	| < tMOD >
	| < tAND >
*/
String operador_multiplicativo() :
{}
{
	try {
		  "*"
		  {
	    	  return "TMS";
		  }
		| "/"
		{
			return "DIV";
		}
		| < tDIV > {
			return "DIV";
		}
		| < tMOD >
		{
			return "MOD";
		}
		| < tAND >
		{
			return "AND";
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
}

/*
 "-" factor()
	| < tNOT > factor()
	| "(" expresion() ")"
	| < tENTACAR > "(" expresion() ")"
	| < tCARAENT > "(" expresion() ")"
	| < tIDENTIFICADOR >
	| < tCONSTENTERA >
	| < tCONSTCHAR >
	| < tCONSTCAD >
	| < tTRUE >
	| < tFALSE >
*/
ExprRegister factor() :
{
	ExprRegister tpFactor = new ExprRegister();
	ExprRegister tpExpr = new ExprRegister();
	ExprRegister tpID = new ExprRegister();
	ExprRegister resultado = new ExprRegister();
	Token to;
	Simbolo s;
}
{
	try {
		  "(" tpExpr = expresion() ")"
		  {
			  return tpExpr;
		  }
		| < tNOT > tpFactor = factor()
		{
			if ((tpFactor.tipo != Tipo_variable.BOOLEANO)
				&& (tpFactor.tipo != Tipo_variable.DESCONOCIDO)) {
				throw new TipoFactorInvalidoException(tpFactor.fila, tpFactor.columna,
				tpFactor.tipo, "booleano, desconocido");
			}

			escribirASM("NGB");

			resultado.val_booleano = !(tpFactor.val_booleano);
			resultado.tipo  = Tipo_variable.BOOLEANO;
			resultado.fila = tpFactor.fila;
			resultado.columna = tpFactor.columna;
			return resultado;
		}
		| "-" tpFactor = factor()
		{
			if ((tpFactor.tipo != Tipo_variable.ENTERO)
				&& (tpFactor.tipo != Tipo_variable.DESCONOCIDO)) {
				throw new TipoFactorInvalidoException(tpFactor.fila, tpFactor.columna,
				tpFactor.tipo, "entero, desconocido");
			}

			escribirASM("SBT");

			resultado.val_entero = tpFactor.val_entero;
			resultado.tipo = Tipo_variable.ENTERO;
			resultado.fila = tpFactor.fila;
			resultado.columna = tpFactor.columna;
			return resultado;
		}
		| < tENTACAR > "(" tpExpr = expresion() ")"
		{
			if ((tpExpr.tipo != Tipo_variable.ENTERO)
				&& (tpExpr.tipo != Tipo_variable.DESCONOCIDO)) {
				throw new TipoFactorInvalidoException(tpFactor.fila, tpFactor.columna,
				tpFactor.tipo, "entero, desconocido");
			}

			resultado.val_char = (char)tpExpr.val_entero;
			resultado.tipo = Tipo_variable.CHAR;
			resultado.fila = tpExpr.fila;
			resultado.columna = tpExpr.columna;
			return resultado;
		}
		| < tCARAENT > "(" expresion() ")"
		{
			if ((tpExpr.tipo != Tipo_variable.CHAR)
				&& (tpFactor.tipo != Tipo_variable.DESCONOCIDO)) {
				throw new TipoFactorInvalidoException(tpFactor.fila, tpFactor.columna,
				tpFactor.tipo, "char, desconocido");
			}

			resultado.val_entero = Character.getNumericValue(tpExpr.val_char);
			resultado.tipo = Tipo_variable.ENTERO; // TODO: Mirar fila y columna
			return resultado;
		}
		| to = < tIDENTIFICADOR >
		{
			s = ts.buscar_simbolo(to.image);
			tpID.tipo = s.getVariable(); System.out.println(tpID.tipo);
			tpID.fila = to.beginLine;
			tpID.columna = to.beginColumn;

			escribirASM("SRF " + ts.getNivel() + " " + s.getDir());
			escribirASM("DRF");

			return tpID;
		}
		| to = < tCONSTENTERA >
		{
			resultado.val_entero = Integer.parseInt(to.image);
			resultado.tipo = Tipo_variable.ENTERO;
			resultado.fila = to.beginLine;
			resultado.columna = to.beginColumn;

			escribirASM("STC " + Integer.parseInt(to.image));

			return resultado;
		}
		| to = < tCONSTCHAR >
		{
			resultado.val_char = to.image.charAt(0);
			resultado.tipo = Tipo_variable.CHAR;
			resultado.fila = to.beginLine;
			resultado.columna = to.beginColumn;

			escribirASM("STC " + Integer.parseInt(to.image));

			return resultado;
		}
		| to = < tCONSTCAD > // TODO: Restricción semántica?? Pasar tipo?
		{
		  resultado.val_char = to.image.charAt(0);
		  resultado.tipo = Tipo_variable.CHAR;
		  return resultado;
		}
		// DUDA: (Factorizacion): Ver si se puede escribir una sola vez para TRUE y FALSE
		| to = < tTRUE >
		{
			resultado.val_booleano = Boolean.parseBoolean(to.image);
			resultado.tipo  = Tipo_variable.BOOLEANO;
			resultado.fila = to.beginLine;
			resultado.columna = to.beginColumn;

			escribirASM("STC 1");

			return resultado;
		}
		| to = < tFALSE >
		{
			resultado.val_booleano = Boolean.parseBoolean(to.image);
			resultado.tipo  = Tipo_variable.BOOLEANO;
			resultado.fila = to.beginLine;
			resultado.columna = to.beginColumn;

			escribirASM("STC 0");

			return resultado;
		}
	}
	catch(ParseException e) {
	  	noCodificarASM();
		error_sintactico(e.currentToken, e.expectedTokenSequences, e.tokenImage);

		Token t;
		do {
			t = getNextToken();
		} while (!SintacticoUtils.tokenEsperado(t, e.expectedTokenSequences) && t!=null && t.kind != EOF );
	}
	catch(SimboloNoEncontradoException e) {
	  	noCodificarASM();

		Token t;
	  	t = getNextToken();
		e.restoSimboloNoEncontrado(t.beginLine, t.beginColumn);
		System.out.println(e);
		tpID.tipo = Tipo_variable.DESCONOCIDO;
		return tpID;
	}
	catch(TipoFactorInvalidoException e) {
	 	noCodificarASM();

	 	System.out.println(e);
	}
}
